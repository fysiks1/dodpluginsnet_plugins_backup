/* Plugin generated by AMXX-Studio */
////////////////////////////////////////////////////////////////////////////////////////////////////
/*

[B]INFO:[/B]

This will make weapons have a better registration. Original made by diamond-optic!



[B]CVARS:[/B]


- Plugin on/off | 1 = on
dod_imp_reg 1


When you set one of these cvars below. The plugin will pick a number between 1 and "what ever you set the cvar"
AND when 1 is picked the registration will be better. If you are one damn unlucky guy you will never get
better registration. But no one will ever be that unlucky. So a greater number = less chance for good
registration.


- Better reg for pistols
dod_imp_reg_pistols 6
( Affected weapons are: luger, webley and colt )


- Better reg for rifles
dod_imp_reg_rifles 2
( Affected weapons are: garand, k98, lee enfield, k43, springfield, scoped k98, scoped lee enfield,
and carbines )


- Better reg for autos
dod_imp_reg_autos 4
( Affected weapons are: thompson, mp40, greasegun, sten and stg44 )


- Better reg for heavy weapons
dod_imp_reg_heavys 8
( Affected weapons are: 30.cal, MG's, FG's, bar and bren )



[B]THANKS TO:[/B]

diamond-optic(www.avamods.com)



[B]CHANGELOG:[/B]


22-07-2009
Release

*/
////////////////////////////////////////////////////////////////////////////////////////////////////
#include <amxmodx>
#include <dodx>
#include <fakemeta>
////////////////////////////////////////////////////////////////////////////////////////////////////
#define PLUGIN "DoD Improved Registry"
#define VERSION "v1.0 by AMXX DoD Team"
#define AUTHOR "AMXX DoD Team"
////////////////////////////////////////////////////////////////////////////////////////////////////
new p_reg, p_heavy, p_rifles, p_auto, p_pistols
new chance[33]
////////////////////////////////////////////////////////////////////////////////////////////////////
public plugin_init()
{
	register_plugin(PLUGIN, VERSION, AUTHOR)
	register_cvar("dod_ir_stats",VERSION,FCVAR_SERVER|FCVAR_SPONLY)
	
	register_forward(FM_TraceLine,"func_TraceLine",1)
	
	p_reg = register_cvar("dod_imp_reg","1")
	
	p_pistols = register_cvar("dod_imp_reg_pistols","6")
	p_rifles = register_cvar("dod_imp_reg_rifles","2")
	p_auto = register_cvar("dod_imp_reg_autos","4")
	p_heavy = register_cvar("dod_imp_reg_heavys","8")

	register_event("CurWeapon", "current_wpn", "be", "1=1")
}
////////////////////////////////////////////////////////////////////////////////////////////////////
public current_wpn(id)
{ 
	if(get_pcvar_num(p_reg) && !is_user_bot(id))
	{
		static weapon ; weapon = read_data(2)
		
		switch(weapon) 
		{
			case DODW_AMERKNIFE, DODW_GARAND_BUTT, DODW_GERKNIFE, DODW_ENFIELD_BAYONET, DODW_SPADE, DODW_KAR_BAYONET, DODW_BRITKNIFE, DODW_K43_BUTT :
				chance[id] = 0
			
			case DODC_30CAL, DODW_BAR, DODW_BREN, DODW_FG42, DODW_MG34, DODW_MG42, DODW_SCOPED_FG42:
				chance[id] = get_pcvar_num(p_heavy)
			
			case DODW_ENFIELD, DODW_GARAND, DODW_K43, DODW_KAR, DODW_SCOPED_ENFIELD, DODW_SCOPED_KAR, DODW_SPRINGFIELD, DODW_M1_CARBINE, DODW_FOLDING_CARBINE:
				chance[id] = get_pcvar_num(p_rifles)
			
			case DODW_GREASEGUN, DODW_MP40, DODW_STEN, DODW_STG44:
				chance[id] = get_pcvar_num(p_auto)
			
			case DODW_COLT, DODW_WEBLEY, DODW_LUGER:
				chance[id] = get_pcvar_num(p_pistols)
			
			default: chance[id] = 0
		}
	}
}
////////////////////////////////////////////////////////////////////////////////////////////////////
public func_TraceLine(Float:v1[3], Float:v2[3], noMonsters, id, ptr)
{
	if(!get_pcvar_num(p_reg) || !is_user_connected(id) || !is_user_alive(id) && is_user_bot(id) || !chance[id])
		return FMRES_IGNORED
	
	static button ; button = pev(id,pev_button)
	
	if(button & IN_ATTACK)
	{
		if(random_num(1, chance[id]) == 1)
		{
			//get aim origin
			static origin[3],Float:fl_origin[3]
			get_user_origin(id, origin, 3)
			IVecFVec(origin, fl_origin)
			
			//set crosshair aim
			set_tr2(ptr, TR_vecEndPos, fl_origin)
			
			//get ent looking at
			static ent, body
			get_user_aiming(id, ent, body)
			
			//if looking at something
			if(pev_valid(ent) && is_user_alive(ent))
			{
				set_tr2(ptr,TR_flFraction, 0.1)          //1.0 == no hit, < 1.0 == hit
				set_tr2(ptr,TR_pHit, ent)                //entity hit
				set_tr2(ptr,TR_iHitgroup, body)          //bodypart hit
			}
			//client_print(id,print_chat,"improved reg!")
		}
	}
	return FMRES_IGNORED
}
////////////////////////////////////////////////////////////////////////////////////////////////////
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1030\\ f0\\ fs16 \n\\ par }
*/
